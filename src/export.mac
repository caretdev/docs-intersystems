ROUTINE export
export() {
  new $namespace
  set $namespace = "%SYS"
  set rs = ##class(%Dictionary.ClassDefinition).ExtentFunc()
  set folder = "/home/irisowner/data/"
  set folder = folder _$piece(##class(%SYSTEM.Version).GetNumber(), ".", 1, 2) _ "/"
  while rs.%Next() {
    set name = rs.ID
    set obj = ##class(%Dictionary.ClassDefinition).%OpenId(name)
    continue:'$isobject(obj)
    continue:obj.Hidden
    continue:obj.GeneratedBy'=""
    continue:$piece(name,".")="csr"
    continue:$piece(name,".")="csp"
    set fileName = folder _ $translate(name, ".", "/") _ ".cls"
    set fileFolder = ##class(%File).GetDirectory(fileName)
    if '##class(%File).DirectoryExists(fileFolder) {
      do ##class(%File).CreateDirectoryChain(fileFolder)
    }
    set stream = ##class(%Stream.FileCharacter).%New()
    set stream.Filename = fileName
    do exportClass(name, stream)
    write !, fileName
    do stream.%Save()

    #; quit
  }
}

exportClass(name="", stream = "") public {
  quit:name=""
  if $piece(name, ".", *) '= "cls" {
    set name = name _ ".cls"
  }
  if '$isobject(stream) {
    set stream = ##class(%Stream.FileCharacter).%New()
  }
  set members = $listbuild(
    "Property",
    "Parameter",
    "Relationship",
    "XData",
    "ClassMethod",
    "Method",
    "ClientMethod",
    "Trigger",
  )

  set tSC = ##class(%Atelier.v1.Utils.TextServices).GetTextAsArray(name,0,.lines)
  set blockOpen = 0, commentOpen = 0, inClass = 0
  for i=1:1:lines(0) {
    set line = $zstrip(lines(i), ">WC")
    if 'inClass,$piece(line," ")="Class" {
      set line = line _ $char(13,10) _ "{" _ $char(13,10)
      do stream.WriteLine(line)
      set inClass = 1
      continue
    }
    continue:'inClass&&($piece(line," ")'="///")
    set ok = 0
    set:'ok ok = ($piece(line," ") = "///")
    if 'ok, $listfind(members, $piece(line, " ")) {
      set ok = 1
      if $get(lines(i+1)) = "{" {
        set line = line _ $char(13, 10) _ "{" _ $char(13, 10) _ "}"
      }
      set line = line _ $char(13, 10)
    }
    continue:'ok
    #; if $listfind()$piece(line, " ")
    do stream.WriteLine(line)
  }
  do stream.WriteLine("}")
}
